apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "cn.jessie.sample.host"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi", "x86"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.jniLibs.srcDirs = ['libs']
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':jessie')
//    implementation 'com.github.7hens:jessie:0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-rc01'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation "io.reactivex.rxjava2:rxjava:2.2.10"
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.github.tbruyelle:rxpermissions:0.10.2'
    implementation 'com.github.7hens:logdog:0.4'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.4'
}

def shell = { String cmd ->
    println "\$ $cmd"
    def process = cmd.execute()
    def thread = { runnable -> new Thread(runnable).start() }
    thread { process.inputStream.eachLine { println ">> $it" } }
    thread { process.errorStream.eachLine { System.err.println ">> $it" } }
    process.waitForOrKill(10000)
}

task installPlugin {
    def pluginApkPath = rootProject.file("sample-plugin/build/outputs/apk/debug/sample-plugin-debug.apk")

    group = 'jessie'
    dependsOn ":sample-plugin:assembleDebug"
    doLast {
        shell "adb push $pluginApkPath /sdcard/jc_plugins/cn.jessie.sample.plugin.apk"
        shell "adb shell am force-stop cn.jessie.sample.host"
        shell "adb shell am start -n cn.jessie.sample.host/.ProgramListActivity"
        println '>> ** install success ** <<'
    }
}