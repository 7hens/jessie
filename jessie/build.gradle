apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'
apply from: 'components.gradle'

group = "com.github.7hens"
version = "-SNAPSHOT"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
        disable 'WrongConstant', "Deprecation"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly project(':android-hide')
    implementation 'com.github.7hens:okbinder:1.0'
    implementation 'cn.thens:okparcelable:0.1.0'
    implementation 'com.github.7hens:logdog:0.4'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'org.robolectric:robolectric:4.2.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

def createUpgradeTask = { String name, String project ->
    def dependency = rootProject.tasks.findByPath(":$project:dexDebug")
    def outputDir = file("src/main/assets/jessie")
    task(type: Jar, dependsOn: dependency.path, group: 'jessie', name) {
        from dependency.outputs.files.singleFile
        archiveFileName = "${project}.jar"
        destinationDirectory = outputDir
    }
}

createUpgradeTask('upgradeHook', 'android-hook')
createUpgradeTask('upgradeRuntime', 'jessie-runtime')
